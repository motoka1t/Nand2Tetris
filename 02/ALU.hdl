// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:
   // zero the x input
   Mux16(a=x, b[0..15]=false, sel=zx, out=w1);
   // negate the x input
   Not16(in=w1, out=w2); 
   Mux16(a=w1, b=w2, sel=nx, out=w3);
   // zero the y input
   Mux16(a=y, b[0..15]=false, sel=zy, out=w4);
   // negate the y input
   Not16(in=w4, out=w5);
   Mux16(a=w4, b=w5, sel=ny, out=w6);
   // compute out=x + y (if f=1) or out=x&y (if f=0)
   And16(a=w3, b=w6, out=w7);
   Add16(a=w3, b=w6, out=w8);
   Mux16(a=w7, b=w8, sel=f, out=w9);
   // negate the out output
   Not16(in=w9, out=w10);
   Mux16(a=w9, b=w10, sel=no, out[0..7]=w11, out[8..15]=w12, out[15]=ng, out=out);
   // zr=1 if out==0, 0 otherwise
   Or8Way(in=w11, out=w13);
   Or8Way(in=w12, out=w14);
   Or(a=w13, b=w14, out=w15);
   Not(in=w15, out=zr);
}